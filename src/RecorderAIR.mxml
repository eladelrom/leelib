<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" 
			   minWidth="400" minHeight="400" 
			   creationComplete="creationCompleteHandler()">
	
	<fx:Script>
		<![CDATA[
			import leelib.util.flvEncoder.MicRecorderUtil;
			
			import recorder.RecorderHelper;
			import recorder.model.RecordVO;
			
			private var camera:Camera;
			private var video:Video;
			private var netConnection:NetConnection;
			private var ns:NetStream;
			
			private var recordVO:RecordVO;
			private var isStreamToLocalFile:Boolean = true;
			
			protected function creationCompleteHandler():void
			{	
				recordVO = new RecordVO(320,240,15,0,0,null,container,outputCallBackFunction,true,true,null,null,0,this);
				// recordVO.encodeAudio = false;
				
				// video
				video = new Video();
				videoContainer.addChild(video);
				netConnection = new NetConnection();
				netConnection.connect(null);
				ns = new NetStream(netConnection);
				camera = Camera.getCamera();
				camera.setMode(recordVO.outputWidth,recordVO.outputHeight, 30);
				camera.setQuality(0, 100);
				camera.addEventListener(StatusEvent.STATUS, onCamStatus);
				camera.addEventListener(ActivityEvent.ACTIVITY, onCamActivity);
				video.attachCamera(camera);				
				
				// audio
				var mic:Microphone = Microphone.getMicrophone();
				mic.setSilenceLevel(0, int.MAX_VALUE);
				mic.gain = 100;
				mic.rate = 44;
				recordVO.micUtil = new MicRecorderUtil(mic);			
			}	
			
			private function outputCallBackFunction(message:String):void {
				output.text = message;
			}
			
			private function onCamStatus(event:StatusEvent):void {
				// implement
			}
			
			private function onCamActivity(event:ActivityEvent):void {
				camera.removeEventListener(StatusEvent.STATUS, onCamStatus);
				camera.removeEventListener(ActivityEvent.ACTIVITY, onCamActivity);
				video.attachCamera(camera);
			}
			
			private function startRecording():void {
				
				recordVO.micUtil.record();
				
				recordVO.bitmaps = new Array();
				recordVO.startTime = getTimer();
				
				if (isStreamToLocalFile)
					RecorderHelper.captureToFile(recordVO);
				else 
					RecorderHelper.captureToMemory(recordVO);
				
			}
			
			private function recordButtonHandler(event:MouseEvent):void {
				if (recordBtn.label == 'Record') {
					// recording
					recordBtn.label = "Stop"
					startRecording();
				}
				else if (recordBtn.label == 'Stop') {
					clearTimeout(recordVO.timeoutId);
					container.alpha = 0.5;
					recordBtn.label = "Record";
					
					// encoding
					if (!isStreamToLocalFile)
						RecorderHelper.startEncoding();
					else
						RecorderHelper.closeFile();
				}
			}
			
			private function saveButtonHandler(event:MouseEvent):void {
				var fileRef:FileReference = new FileReference();
				fileRef.save(RecorderHelper.recordVO.byteArrayFlvEncoder.byteArray, "recording.flv");			
			}			
			
		]]>
	</fx:Script>
	
	<s:BorderContainer id="container" height="260" width="320" x="10" y="40">
		<s:Label id="output" />
		<mx:UIComponent id="videoContainer" height="240" width="320" x="0" y="10"/>
	</s:BorderContainer>
	
	<s:Button id="recordBtn" label="Record" click="recordButtonHandler(event)" y="10" />
	<s:Button label="Save" click="saveButtonHandler(event)" x="80" y="10" />
	
</s:WindowedApplication>
